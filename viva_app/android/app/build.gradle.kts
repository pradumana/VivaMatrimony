plugins {
    id("com.android.application")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}

android {
    namespace = "com.vivamatrimony.viva_app"
    compileSdk = 36
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    signingConfigs {
        create("release") {
            // Set via environment variables or key.properties file
            // See docs/DEPLOYMENT.md for setup instructions
            val keystoreFile = System.getenv("KEYSTORE_PATH") ?: project.findProperty("keystorePath") as String?
            val keystorePass = System.getenv("KEYSTORE_PASSWORD") ?: project.findProperty("keystorePassword") as String?
            val keyAliasName = System.getenv("KEY_ALIAS") ?: project.findProperty("keyAlias") as String?
            val keyPass = System.getenv("KEY_PASSWORD") ?: project.findProperty("keyPassword") as String?

            if (keystoreFile != null) {
                storeFile = file(keystoreFile)
                storePassword = keystorePass
                keyAlias = keyAliasName
                keyPassword = keyPass
            }
        }
    }

    defaultConfig {
        applicationId = "com.vivamatrimony.viva_app"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    buildTypes {
        release {
            signingConfig = if (System.getenv("KEYSTORE_PATH") != null || project.hasProperty("keystorePath"))
                signingConfigs.getByName("release")
            else
                signingConfigs.getByName("debug") // fallback for local dev
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
    }
}

flutter {
    source = "../.."
}
